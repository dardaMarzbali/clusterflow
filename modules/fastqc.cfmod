#!/usr/bin/perl
use warnings;
use strict;
use Getopt::Long;
use FindBin qw($Bin);
use lib "$FindBin::Bin/../source";
use CF::Constants;
use CF::Helpers;

# Get Options
my $required_cores;
my $required_mem;
my $required_modules;
my $help;
my $result = GetOptions ("cores=i" => \$required_cores, "mem" => \$required_mem, "modules" => \$required_modules, "help" => \$help);

# QSUB SETUP
# --cores i = offered cores. Return number of required cores.
if($required_cores){
	print 1;
	exit;
}
# --mem. Return the required memory allocation.
if($required_mem){
	print '1G';
	exit;
}
# --modules. Return csv names of any modules which should be loaded.
if($required_modules){
	print 'fastqc';
	exit;
}
# --help. Print help.
if($help){
	print "".("-"x15)."\n FastQC Module\n".("-"x15)."\n
FastQC is a quality control tool for high throughput sequence data.
For further information, please run fastqc --help\n\n";
	exit;
}

# MODULE
my $timestart = time;

# Read in the input files from the run file
my ($files, $runfile, $job_id, $prev_job_id, $cores, $mem, $parameters, $config_ref) = CF::Helpers::load_runfile_params(@ARGV);
my %config = %$config_ref;

open (RUN,'>>',$runfile) or die "Can't write to $runfile: $!";

# Read any options from the pipeline parameters
my $nogroup = "";
foreach my $parameter (@$parameters){
	if($parameter eq "nogroup"){
		$nogroup = "--nogroup";
	}
}


# Go through each supplied file and run FastQC.
foreach my $file (@$files){
	
	my $command = "fastqc $nogroup $file";
	warn "\n###CFCMD $command\n\n";
	
	if(!system ($command)){
		print RUN "$job_id\t$file\n";
		my $duration =  CF::Helpers::parse_seconds(time - $timestart);
		warn "###CF FastQC successfully ran, took $duration\n";
	} else {
		print "###CF ## FastQC Failed: $? $!\n";
	}
}

close (RUN);