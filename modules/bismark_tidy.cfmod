#!/usr/bin/perl
use warnings;
use strict;
use Getopt::Long;
use FindBin qw($Bin);
use lib "$FindBin::Bin/../source";
use CF::Constants;
use CF::Helpers;
use File::Copy qw(move);

##########################################################################
# Copyright 2014, Philip Ewels (phil.ewels@babraham.ac.uk)               #
#                                                                        #
# This file is part of Cluster Flow.                                     #
#                                                                        #
# Cluster Flow is free software: you can redistribute it and/or modify   #
# it under the terms of the GNU General Public License as published by   #
# the Free Software Foundation, either version 3 of the License, or      #
# (at your option) any later version.                                    #
#                                                                        #
# Cluster Flow is distributed in the hope that it will be useful,        #
# but WITHOUT ANY WARRANTY; without even the implied warranty of         #
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the          #
# GNU General Public License for more details.                           #
#                                                                        #
# You should have received a copy of the GNU General Public License      #
# along with Cluster Flow.  If not, see <http://www.gnu.org/licenses/>.  #
##########################################################################


# Get Options
my $required_cores;
my $required_mem;
my $required_modules;
my $help;
my $result = GetOptions ("cores=i" => \$required_cores, "mem" => \$required_mem, "modules" => \$required_modules, "help" => \$help);

# QSUB SETUP
# --cores i = offered cores. Return number of required cores.
if($required_cores){
	print 1;
	exit;
}
# --mem. Return the required memory allocation.
if($required_mem){
	print '100M';
	exit;
}
# --modules. Return csv names of any modules which should be loaded.
if($required_modules){
	print 'bismark';
	exit;
}
# --help. Print help.
if($help){
	print "".("-"x21)."\n Bismark Tidy Module\n".("-"x21)."\n
This script takes the bismark output, creates a directory
based on the filenames and moves everything into that directory.
It then runs the bismark2report script to generate an overview report.\n
For further information, please run bismark2report --help\n\n";
	exit;
}

# MODULE
# Read in the input files from the run file
my ($files, $runfile, $job_id, $prev_job_id, $cores, $mem, $parameters, $config_ref) = CF::Helpers::load_runfile_params(@ARGV);

# Figure out directory names
my %dirs;
foreach my $file (@$files){
	$file =~ s/(\.)?(_R[12])?(r_[12])?(_val_[12])?\.f(ast)?q.+//i; # strip stuff from end of filename
	$file =~ s/^(CpG|Non_CpG|CHG|CHH)_//i; # strip stuff from start of filename
	$file =~ s/^[COBT]+_//;
	$dirs{$file} = 1;
}

# Get current directory listing
my @dirfiles = glob('*');

foreach my $dir (keys (%dirs)){

	# make directory
	mkdir $dir;

	# move matching files into this directory
	my $count = 0;
	foreach my $dfile (@dirfiles) {
		if($dfile =~ /$dir/i){
			unless($dfile =~ /\.run$/i || $dfile =~ /\.out$/i){
				move ($dfile, $dir."/".$dfile);
				$count++;
			}
		}
	}
	warn "###CF $count files tidied into bismark folder\n";

	# run bismark2report in directory
	chdir ($dir);
	if(!system ("bismark2report")){
		warn "###CF Bismark report successfully created\n";
	} else {
		warn "###CF Bismark report exited with an error state\n";
	}
	chdir ("../");

}
