#!/usr/bin/perl
use warnings;
use strict;
use Getopt::Long;
use FindBin qw($Bin);
use lib "$FindBin::Bin/../source";
use CF::Constants;
use CF::Helpers;

# Get Options
my $required_cores;
my $required_mem;
my $required_modules;
my $help;
my $result = GetOptions ("cores=i" => \$required_cores, "mem" => \$required_mem, "modules" => \$required_modules, "help" => \$help);

# QSUB SETUP
# --cores i = offered cores. Return number of required cores.
if($required_cores){
	print 3;
	exit;
}
# --mem. Return the required memory allocation.
if($required_mem){
	print '3G';
	exit;
}
# --modules. Return csv names of any modules which should be loaded.
if($required_modules){
	print 'bismark';
	exit;
}
# --help. Print help.
if($help){
	print "The bismark_align module runs the main bismark script.
Bismark is a program to map bisulfite treated sequencing reads
to a genome of interest and perform methylation calls.\n
Use bismark --help for further information.\n\n";
	exit;
}

# MODULE
# Read in the input files from the run file
my ($files, $runfile, $job_id, $prev_job_id, $cores, $mem, $parameters, $config_ref) = CF::Helpers::load_runfile_params(@ARGV);
my %config = %$config_ref;

# Check that we have a genome defined
if(!defined($config{genome_path})){
	warn "\n\n###CF No genome path found in run file $runfile for job $job_id. Exiting.. ###\n\n";
	exit;
}

open (RUN,'>>',$runfile) or die "Can't write to $runfile: $!";

# Separate file names into single end and paired end
my ($se_files, $pe_files) = CF::Helpers::is_paired_end(\%config, @$files);

# FastQ encoding type. Once found on one file will assume all others are the same
my $encoding = 0;

# Read any options from the pipeline parameters
my $bt2 = "";
my $pbat = "";
foreach my $parameter (@$parameters){
	if($parameter eq "pbat"){
		$pbat = "--pbat";
	}
	if($parameter eq "bt2"){
		$bt2 = "--bowtie2";
	}
}


# Go through each single end files and run Bismark
if($se_files && scalar(@$se_files) > 0){
	foreach my $file (@$se_files){
		
		# Figure out the encoding if we don't already know
		if(!$encoding){
			($encoding) = CF::Helpers::fastq_encoding_type($file);
		}
		my $enc = "";
		if($encoding eq 'phred33' || $encoding eq 'phred64' || $encoding eq 'solexa'){
			$enc = '--'.$encoding.'-quals';
		}
		
		my $output_fn;
		if($bt2){
			$output_fn = $file."_bismark_bt2.bam";
		} else {
			$output_fn = $file."_bismark.bam";
		}
		
		my $command = "bismark --bam $bt2 $pbat $enc ".$config{genome_path}." ".$file;
		warn "\nRunning command:\n$command\n\n";
		
		if(!system ($command)){
			# Bismark worked - print out resulting filenames
			warn "###CF Bismark (SE mode) successfully exited..\n";
			if(-e $output_fn){
				print RUN "$job_id\t$output_fn\n"; 
			} else {
				warn "\nBismark output file $output_fn not found..\n";
			}
		} else {
			die "###CF ## Bismark alignment (SE mode) exited with an error state\n\n";
		}
	}
}

# Go through the paired end files and run Bismark
if($pe_files && scalar(@$pe_files) > 0){
	foreach my $files_ref (@$pe_files){
		my @files = @$files_ref;
		if(scalar(@files) == 2){
			
			# Figure out the encoding if we don't already know
			if(!$encoding){
				($encoding) = CF::Helpers::fastq_encoding_type($files[0]);
			}
			my $enc = "";
			if($encoding eq 'phred33' || $encoding eq 'phred64' || $encoding eq 'solexa'){
				$enc = '--'.$encoding.'-quals';
			}
			
			my $output_fn;
			if($bt2){
				$output_fn = $files[0]."_bismark_pe_bt2.bam";
			} else {
				$output_fn = $files[0]."_bismark_pe.bam";
			}
			
			my $command = "bismark --bam $bt2 $pbat $enc ".$config{genome_path}." -1 ".$files[0]." -2 ".$files[1];
			warn "\nRunning command:\n$command\n\n";
			
			if(!system ($command)){
				# Bismark worked - print out resulting filenames
				warn "###CF Bismark (PE mode) successfully exited..\n";
				if(-e $output_fn){
					print RUN "$job_id\t$output_fn\n";
				} else {
					warn "\nBismark output file $output_fn not found..\n";
				}
			} else {
				die "###CF ## Bismark alignment (PE mode) exited with an error state\n\n";
			}
		} else {
			warn "\n###CF ## Bismark paired end files had ".scalar(@files)." input files instead of 2..\n";
		}
	}
}


close (RUN);